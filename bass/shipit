#!/usr/bin/env bass

; load libraries
(use (.strings)
     (*dir*/bass.bass))

; builds and publishes a GitHub release
;
; Needs the tag to checkout, build, and publish along with a title for the
; release.
(defn main []
  (for [{:sha sha
         :tag tag
         (:title tag) title
         (:draft false) draft} *stdin*]
    (let [src (bass:checkout sha)
          release-url (create-release src sha tag title draft)]
      (log "release published" :release release-url))))

; returns true if the tag looks like a prerelease version
(defn prerelease? [tag]
  (or ; "v1.2.3-rc.1"
      (strings:includes? tag "-")
      ; "v0.2.0+dev"
      (strings:includes? tag "+")
      ; "nightly"
      (not (strings:includes? tag "."))))

; creates a release with the given assets
(defn create-release [src sha tag title draft]
  (let [release (bass:release (mask *env*:GITHUB_TOKEN :github-token))
        assets (bass:release-assets src tag)
        draft draft
        pre? (prerelease? tag)]
    (log "shipping!" :sha sha :tag tag)
    (release:create!
      tag assets
      :target sha
      :title title
      :generate-notes true
      :notes-file (bass:release-notes src tag)
      :draft draft
      :prerelease pre?
      :discussion-category (if (or draft pre?) null "General"))))
